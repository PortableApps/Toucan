cmake_minimum_required(VERSION 2.6)

set(CMAKE_CONFIGURATION_TYPES "Debug;Release" CACHE STRING "" FORCE)
set(CMAKE_MODULE_PATH "${CMAKE_CURRENT_SOURCE_DIR}")

project(Toucan)

#Find wxWidgets
find_package(wxWidgets COMPONENTS base core net adv html aui stc REQUIRED)
include_directories(${wxWidgets_INCLUDE_DIRS})
add_definitions(${wxWidgets_DEFINITIONS})

#Define unicode if we need to
if(wxWidgets_CONFIGURATION MATCHES "u")
    add_definitions(-DUNICODE)
endif(wxWidgets_CONFIGURATION MATCHES "u")

#Find Lua
find_package(Lua51 REQUIRED)
include_directories(${LUA_INCLUDE_DIR})

#Find SWIG
find_package(SWIG REQUIRED)

#Find cxxtest
find_package(CxxTest)
include_directories(${CXXTEST_INCLUDE_DIR})

if(MSVC)
    #Disable security warnings
    add_definitions(-D_CRT_SECURE_NO_WARNINGS)
    #Set the warning level to maximum
    if(CMAKE_CXX_FLAGS MATCHES "/W[0-4]")
        string(REGEX REPLACE "/W[0-4]" "/W4" CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS}")
    endif(CMAKE_CXX_FLAGS MATCHES "/W[0-4]")

    #We statically link to reduce dependancies
    foreach(flag_var CMAKE_CXX_FLAGS CMAKE_CXX_FLAGS_DEBUG CMAKE_CXX_FLAGS_RELEASE CMAKE_C_FLAGS CMAKE_C_FLAGS_DEBUG CMAKE_C_FLAGS_RELEASE)
        if(${flag_var} MATCHES "/MD")
            string(REGEX REPLACE "/MD" "/MT" ${flag_var} "${${flag_var}}")
        endif(${flag_var} MATCHES "/MD")
        if(${flag_var} MATCHES "/MDd")
            string(REGEX REPLACE "/MDd" "/MTd" ${flag_var} "${${flag_var}}")
        endif(${flag_var} MATCHES "/MDd")
    endforeach(flag_var)
endif(MSVC)

#Traverse through the sub-directories
add_subdirectory(backup)
add_subdirectory(controls)
add_subdirectory(data)
add_subdirectory(forms)
add_subdirectory(secure)
add_subdirectory(sync)

#Link against them
link_directories(backup)
link_directories(controls)
link_directories(data)
link_directories(forms)
link_directories(secure)
link_directories(sync)

#test.cpp is generated by cxxtest
set_source_files_properties(test.cpp PROPERTIES GENERATED true)
#toucan_wrap.cpp is generated by SWIG
set_source_files_properties(toucan_wrap.cpp PROPERTIES GENERATED true)

#Add the source and header files
set(source ${source} basicfunctions.cpp cmdline.cpp dragndrop.cpp filecounter.cpp)
set(source ${source} job.cpp luamanager.cpp luathread.cpp rules.cpp settings.cpp)
set(source ${source} signalprocess.cpp test.cpp toucan.cpp toucan_wrap.cpp variables.cpp)

set(headers ${headers} basicfunctions.h cmdline.h dragndrop.h filecounter.h)
set(headers ${headers} job.h luamanager.h luathread.h rules.h settings.h)
set(headers ${headers} signalprocess.h test.h toucan.h variables.h)
set(headers ${headers} toucan.i typemaps.i)

if(WIN32)
    #Add the resource file for the icon
    set(source ${source} toucan.rc)
endif(WIN32)

#Generate toucan_wrap.cpp using swig
add_custom_command(
    OUTPUT  ${CMAKE_CURRENT_SOURCE_DIR}/toucan_wrap.cpp
    DEPENDS ${CMAKE_CURRENT_SOURCE_DIR}/toucan.i 
    COMMAND ${SWIG_EXECUTABLE} -o ${CMAKE_CURRENT_SOURCE_DIR}/toucan_wrap.cpp -lua -c++ ${CMAKE_CURRENT_SOURCE_DIR}/toucan.i
)

#Generate test.cpp using cxxtest
add_custom_command(
    OUTPUT  ${CMAKE_CURRENT_SOURCE_DIR}/test.cpp
    DEPENDS ${CMAKE_CURRENT_SOURCE_DIR}/test.h
    COMMAND ${CXXTEST_PYTHON_TESTGEN_EXECUTABLE} -o ${CMAKE_CURRENT_SOURCE_DIR}/test.cpp ${CMAKE_CURRENT_SOURCE_DIR}/test.h
)

#Set up unit tests
enable_testing()
add_test(test toucan unittests)

#Set up the exe
add_executable(toucan ${source} ${headers})
target_link_libraries(toucan backup controls data forms secure sync ${wxWidgets_LIBRARIES} ${LUA_LIBRARIES})

if(MSVC)
    #Disable manifest creation as we have our own .rc file
	set_target_properties(toucan PROPERTIES LINK_FLAGS "/MANIFEST:NO")
endif(MSVC)

#Once we have built Toucan move it somewhere suitable
set(Toucan_Output "C:\\" CACHE PATH "The path to an existing Toucan installation")
set(Toucan_Exepath ${CMAKE_BINARY_DIR}/${CMAKE_CFG_INTDIR}/toucan${CMAKE_EXECUTABLE_SUFFIX})
add_custom_command(
    TARGET toucan
    POST_BUILD
    COMMAND ${CMAKE_COMMAND} -E copy ${Toucan_Exepath} ${Toucan_Output}/App/toucan/toucan${CMAKE_EXECUTABLE_SUFFIX}
)
